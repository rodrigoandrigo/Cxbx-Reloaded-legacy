#include "CxbxFixedFunctionVertexShaderState.hlsli"

#include "CxbxVertexShaderCommon.hlsli"
#include "CxbxVertexFetch.hlsli"

// Whether each vertex register is present in the vertex declaration.
// Used by DoMaterial to determine ColorVertex behavior.
static  bool  vRegisterDefaultFlags[16];

uniform FixedFunctionVertexShaderState state : register(c0);

// Input register indices (also known as attributes, as given in VS_INPUT.v array)
// TODO : Convert FVF codes on CPU to a vertex declaration with these standardized register indices:
// NOTE : Converting FVF vertex indices must also consider NV2A vertex attribute 'slot mapping',
// as set in NV2A_VTXFMT/NV097_SET_VERTEX_DATA_ARRAY_FORMAT!
// TODO : Rename these into SLOT_POSITION, SLOT_WEIGHT, SLOT_TEXTURE0, SLOT_TEXTURE3, etc :
static const uint position = 0;     // See X_D3DFVF_XYZ      / X_D3DVSDE_POSITION    was float4 pos : POSITION;
static const uint weight = 1;       // See X_D3DFVF_XYZB1-4  / X_D3DVSDE_BLENDWEIGHT was float4 bw : BLENDWEIGHT;
static const uint normal = 2;       // See X_D3DFVF_NORMAL   / X_D3DVSDE_NORMAL      was float4 normal : NORMAL; // Note : Only normal.xyz is used. 
static const uint diffuse = 3;      // See X_D3DFVF_DIFFUSE  / X_D3DVSDE_DIFFUSE     was float4 color[2] : COLOR;
static const uint specular = 4;     // See X_D3DFVF_SPECULAR / X_D3DVSDE_SPECULAR
static const uint fogCoord = 5;     // Has no X_D3DFVF_* ! See X_D3DVSDE_FOG         Note : Only fog.x is used.
static const uint pointSize = 6;    // Has no X_D3DFVF_* ! See X_D3DVSDE_POINTSIZE
static const uint backDiffuse = 7;  // Has no X_D3DFVF_* ! See X_D3DVSDE_BACKDIFFUSE was float4 backColor[2] : TEXCOORD4;
static const uint backSpecular = 8; // Has no X_D3DFVF_* ! See X_D3DVSDE_BACKSPECULAR
static const uint texcoord0 = 9;    // See X_D3DFVF_TEX1     / X_D3DVSDE_TEXCOORD0   was float4 texcoord[4] : TEXCOORD;
static const uint texcoord1 = 10;   // See X_D3DFVF_TEX2     / X_D3DVSDE_TEXCOORD1
static const uint texcoord2 = 11;   // See X_D3DFVF_TEX3     / X_D3DVSDE_TEXCOORD2
static const uint texcoord3 = 12;   // See X_D3DFVF_TEX4     / X_D3DVSDE_TEXCOORD3
static const uint reserved0 = 13;   // Has no X_D3DFVF_*     / X_D3DVSDE_*
static const uint reserved1 = 14;   // Has no X_D3DFVF_*     / X_D3DVSDE_*
static const uint reserved2 = 15;   // Has no X_D3DFVF_*     / X_D3DVSDE_*

// Vertex attributes fetched from ByteAddressBuffer into this static array;
// VS_INPUT only carries SV_VertexID.
static float4 g_FetchedAttribs[16];

float4 Get(const uint index)
{
    return g_FetchedAttribs[index];
}

struct TransformInfo
{
    float4 Position;
    float3 Normal;
};

static TransformInfo View; // Vertex transformed to viewspace
static TransformInfo Projection; // Vertex transformed to projection space

static const int LIGHT_TYPE_NONE = 0;
static const int LIGHT_TYPE_POINT = 1;
static const int LIGHT_TYPE_SPOT = 2;
static const int LIGHT_TYPE_DIRECTIONAL = 3;

// Final lighting output
struct LightingOutput
{
    TwoSidedColor Diffuse;
    TwoSidedColor Specular;
};

// useful reference https://drivers.amd.com/misc/samples/dx9/FixedFuncShader.pdf
LightingOutput DoLight(const Light l, const float2 powers)
{
    LightingOutput o;
    o.Diffuse.Front = o.Diffuse.Back = float3(0, 0, 0);
    o.Specular.Front = o.Specular.Back = float3(0, 0, 0);

	float3 toLight;
	float3 toLightN;
	float attenuation = 1;
	float spotIntensity = 1;
	
	if (l.Type == LIGHT_TYPE_DIRECTIONAL) {
		toLight = toLightN = l.DirectionVN; // NV2A stores direction TO light (pre-negated by D3D runtime)
	}
	else {
		toLight = l.PositionV - View.Position.xyz;
		toLightN = normalize(toLight);
	}

	if (l.Type == LIGHT_TYPE_SPOT) {
		// Spotlight factors
		// https://docs.microsoft.com/en-us/windows/win32/direct3d9/light-types
		float3 toVertexN = -toLightN;
		float cosAlpha = dot(l.DirectionVN, toVertexN);
		// I = ( cos(a) - cos(phi/2) ) / ( cos(theta/2) - cos(phi/2) )
		float spotBase = saturate((cosAlpha - l.CosHalfPhi) / l.SpotIntensityDivisor);
		spotIntensity = pow(spotBase, l.Falloff);
	}

	if (l.Type == LIGHT_TYPE_POINT || l.Type == LIGHT_TYPE_SPOT) {
		float lightDist = length(toLight);

		// A(Constant) + A(Linear) * dist + A(Exp) * dist^2
		attenuation =
		1 / (l.Attenuation[0]
			+ l.Attenuation[1] * lightDist
			+ l.Attenuation[2] * lightDist * lightDist);

		// Range cutoff
		if (lightDist > l.Range)
			attenuation = 0;
	}

	// Diffuse lighting calculation
	const float NdotLFront = dot(View.Normal, toLightN);
	const float NdotLBack = dot(-View.Normal, toLightN);
	o.Diffuse.Front = max(NdotLFront, 0) * l.Diffuse.rgb * attenuation * spotIntensity;
	o.Diffuse.Back = max(NdotLBack, 0) * l.Diffuse.rgb * attenuation * spotIntensity;

	// Specular lighting calculation
	float3 toViewerN = state.Modes.LocalViewer
		? normalize(-View.Position.xyz) // Strip sample
		: float3(0, 0, -1); // DoA 3 character select
	
	// Note : if X_D3DRS_SPECULARENABLE is false then all light specular colours should have been zeroed out
	// Blinn-Phong
	// https://learnopengl.com/Advanced-Lighting/Advanced-Lighting
	const float3 halfway = normalize(toViewerN + toLightN);
	const float NdotHFront = dot(View.Normal, halfway);
	const float NdotHBack = dot(-View.Normal, halfway);

	o.Specular.Front = pow(max(NdotHFront, 0), powers[0]) * l.Specular.rgb * attenuation * spotIntensity;
	o.Specular.Back = pow(max(NdotHBack, 0), powers[1]) * l.Specular.rgb * attenuation * spotIntensity;

	return o;
}

LightingOutput CalcLighting(const float2 powers)
{
    LightingOutput totalLightOutput;
    totalLightOutput.Diffuse.Front = float3(0, 0, 0);
    totalLightOutput.Diffuse.Back = float3(0, 0, 0);
    totalLightOutput.Specular.Front = float3(0, 0, 0);
    totalLightOutput.Specular.Back = float3(0, 0, 0);

    for (uint i = 0; i < 8; i++)
    {
        const Light currentLight = state.Lights[i];
        LightingOutput currentLightOutput;

		if (currentLight.Type != LIGHT_TYPE_NONE) {
			currentLightOutput = DoLight(currentLight, powers);

			totalLightOutput.Diffuse.Front += currentLightOutput.Diffuse.Front;
			totalLightOutput.Diffuse.Back += currentLightOutput.Diffuse.Back;
			totalLightOutput.Specular.Front += currentLightOutput.Specular.Front;
			totalLightOutput.Specular.Back += currentLightOutput.Specular.Back;
		}
    }

    return totalLightOutput;
}

TransformInfo DoTransform(const float4 position, const float3 normal, const float4 blendWeights)
{
    TransformInfo output;
    output.Position = float4(0, 0, 0, 0);
    output.Normal = float3(0, 0, 0);

    // The number of matrices to blend (always in the range [1..4])
    const int matrices = state.Modes.VertexBlend_NrOfMatrices;

    // Initialize the final matrix its blend weight at 1, from which all preceding blend weights will be deducted :
    float lastBlend = 1;
    for (int i = 0; i < matrices; i++)
    {
        // Do we have to calculate the last blend value (never happens when there's already 4 matrices) ?
        const bool bCalcFinalWeight = (state.Modes.VertexBlend_CalcLastWeight > 0) && (i == (matrices - 1));
        // Note : In case of X_D3DVBF_DISABLE, no prior weights have been deducted from lastBlend, so it will still be 1.
        // The number of matrices will also be 1, which effectively turns this into non-weighted single-matrix multiplications :
        const float blendWeight = bCalcFinalWeight ? lastBlend : blendWeights[i];
        // Reduce the blend weight for the final matrix :
        lastBlend -= blendWeights[i];
        // Add this matrix (multiplied by its blend weight) to the output :
        output.Position += mul(position, state.Transforms.WorldView[i]) * blendWeight;
        output.Normal += mul(normal, (float3x3) state.Transforms.WorldViewInverseTranspose[i]) * blendWeight;
    }

    return output;
}

Material DoMaterial(const uint index, const uint diffuseReg, const uint specularReg)
{
    // Get the material from material state
    Material material = state.Materials[index];

    // Note : if (state.Modes.ColorVertex) no longer required because when disabled, CPU sets all MaterialSource's to D3DMCS_MATERIAL
    {
        // https://docs.microsoft.com/en-us/windows/win32/direct3d9/d3dmaterialcolorsource
        static const int D3DMCS_MATERIAL = 0;
        static const int D3DMCS_COLOR1 = 1;
        static const int D3DMCS_COLOR2 = 2;

        // If COLORVERTEX mode, AND the desired diffuse or specular colour is defined in the vertex declaration
        // Then use the vertex colour instead of the material

        if (!vRegisterDefaultFlags[diffuseReg]) {
            const float4 diffuseVertexColour = Get(diffuseReg);
            if (state.Modes.AmbientMaterialSource == D3DMCS_COLOR1) material.Ambient = diffuseVertexColour;
            if (state.Modes.DiffuseMaterialSource == D3DMCS_COLOR1) material.Diffuse = diffuseVertexColour;
            if (state.Modes.SpecularMaterialSource == D3DMCS_COLOR1) material.Specular = diffuseVertexColour;
            if (state.Modes.EmissiveMaterialSource == D3DMCS_COLOR1) material.Emissive = diffuseVertexColour;
        }

        if (!vRegisterDefaultFlags[specularReg]) {
            const float4 specularVertexColour = Get(specularReg);
            if (state.Modes.AmbientMaterialSource == D3DMCS_COLOR2) material.Ambient = specularVertexColour;
            if (state.Modes.DiffuseMaterialSource == D3DMCS_COLOR2) material.Diffuse = specularVertexColour;
            if (state.Modes.SpecularMaterialSource == D3DMCS_COLOR2) material.Specular = specularVertexColour;
            if (state.Modes.EmissiveMaterialSource == D3DMCS_COLOR2) material.Emissive = specularVertexColour;
        }
    }

    return material;
}

float DoFog()
{
    if (!state.Fog.Enable)
        return 1; // No fog!
    // http://developer.download.nvidia.com/assets/gamedev/docs/Fog2.pdf

    // Obtain the fog depth value 'd'
    float fogDepth = 0;

    if (state.Fog.DepthMode == FixedFunctionVertexShader::FOG_DEPTH_NONE)
        fogDepth = Get(specular).a; // In fixed-function mode, fog is passed in the specular alpha
    else if (state.Fog.DepthMode == FixedFunctionVertexShader::FOG_DEPTH_RANGE)
        fogDepth = length(View.Position.xyz);
    else if (state.Fog.DepthMode == FixedFunctionVertexShader::FOG_DEPTH_Z)
        fogDepth = abs(Projection.Position.z);
    else if (state.Fog.DepthMode == FixedFunctionVertexShader::FOG_DEPTH_W)
        fogDepth = Projection.Position.w;
    else if (state.Fog.DepthMode == FixedFunctionVertexShader::FOG_DEPTH_W_ABS)
        fogDepth = abs(Projection.Position.w);

    // Use NV2A-native FOGPARAM0/1 computation (matches xemu).
    return CalculateFogFactor(state.Fog.FogMode, state.Fog.FogParam0,
                              state.Fog.FogParam1, fogDepth);
}

float4 DoTexCoord(const uint stage)
{
    // Texture transform flags
    // https://docs.microsoft.com/en-gb/windows/win32/direct3d9/d3dtexturetransformflags
    static const int D3DTTFF_DISABLE = 0;
    static const int D3DTTFF_COUNT1  = 1;
    static const int D3DTTFF_COUNT2  = 2;
    static const int D3DTTFF_COUNT3  = 3;
    static const int D3DTTFF_COUNT4  = 4;
    static const int D3DTTFF_PROJECTED = 256; // This is the only real flag

    // https://docs.microsoft.com/en-us/windows/win32/direct3d9/d3dtss-tci
    // Pre-shifted
    static const int TCI_PASSTHRU = 0;
    static const int TCI_CAMERASPACENORMAL = 1;
    static const int TCI_CAMERASPACEPOSITION = 2;
    static const int TCI_CAMERASPACEREFLECTIONVECTOR = 3;
    static const int TCI_OBJECT = 4; // Xbox
    static const int TCI_SPHERE = 5; // Xbox

    const TextureState tState = state.TextureStates[stage];

    // Extract transform flags
    const int countFlag = tState.TextureTransformFlagsCount;
    const bool projected = tState.TextureTransformFlagsProjected;

    // Get texture coordinates
    // Coordinates are either from the vertex texcoord data
    // Or generated
    float4 texCoord = float4(0, 0, 0, 0);
    if (tState.TexCoordIndexGen == TCI_PASSTHRU)
    {
        // Get from vertex data
        const uint texCoordIndex = abs(tState.TexCoordIndex); // Note : abs() avoids error X3548 : in vs_3_0 uints can only be used with known - positive values, use int if possible
        texCoord = Get(texcoord0+texCoordIndex);

        // Make coordinates homogenous
        // For example, if a title supplies (u, v)
        // We need to make transform (u, v, 1) to allow translation with a 3x3 matrix
        // We'll need to get this from the current FVF or VertexDeclaration
        // Test case: JSRF scrolling texture effect.
        // Test case: Madagascar shadows
        // Test case: Modify pixel shader sample

       // TODO move alongside the texture transformation when it stops angering the HLSL compiler
        const float componentCount = state.TexCoordComponentCount[texCoordIndex];
        if (componentCount == 1)
            texCoord.yzw = float3(0, 0, 1);
        if (componentCount == 2)
            texCoord.zw = float2(0, 1);
        if (componentCount == 3)
            texCoord.w = 1;
    }   // Generate texture coordinates
    else if (tState.TexCoordIndexGen == TCI_CAMERASPACENORMAL)
        texCoord = float4(View.Normal, 1);
    else if (tState.TexCoordIndexGen == TCI_CAMERASPACEPOSITION)
        texCoord = mul(View.Position, state.Transforms.TexgenMatrix[stage]);
    else if (tState.TexCoordIndexGen == TCI_OBJECT)
        texCoord = mul(Get(position), state.Transforms.TexgenMatrix[stage]);
    else
    {
        const float3 reflected = reflect(normalize(View.Position.xyz), View.Normal);

        if (tState.TexCoordIndexGen == TCI_CAMERASPACEREFLECTIONVECTOR)
            texCoord.xyz = reflected;
        else if (tState.TexCoordIndexGen == TCI_SPHERE)
        {
            // TODO verify
            // http://www.bluevoid.com/opengl/sig99/advanced99/notes/node177.html
            const float3 R = reflected;
            const float p = sqrt(pow(R.x, 2) + pow(R.y, 2) + pow(R.z + 1, 2));
            texCoord.x = R.x / (2 * p) + 0.5f;
            texCoord.y = R.y / (2 * p) + 0.5f;
        }
    }

    // Transform the texture coordinates if requested
    if (countFlag != D3DTTFF_DISABLE)
        texCoord = mul(texCoord, state.Transforms.Texture[stage]);

    // We always send four coordinates
    // If we are supposed to send less than four
    // then copy the last coordinate to the remaining coordinates
    // For D3DTTFF_PROJECTED, the value of the *last* coordinate is important
    // Test case: ProjectedTexture sample, which uses 3 coordinates
    // We'll need to implement the divide when D3D stops handling it for us?
    // https://docs.microsoft.com/en-us/windows/win32/direct3d9/d3dtexturetransformflags
    // padding makes no differences in LLE. LLE works with ProjectedTexture sample without padding, but to be devided by w is necessary.

    if (projected)
    {
        //if (countFlag == 1)
            //texCoord.yz = texCoord.x;
        //if (countFlag == 2)
            //texCoord.z = texCoord.y;
        //texCoord.xyzw = texCoord.xyzw / texCoord.w;
    }

    return texCoord;
}

// Point size for Point Sprites
// NV2A hardware formula (matches xemu vsh-ff.c):
//   Scaled:    raw = rsqrt(A + B*d + C*d²); oPts = clamp(raw * Scale + Bias, Min, Max) * UpscaleFactor
//   Non-scaled: oPts = clamp(rsqrt(1) * PointSize + 0, 1, 63.875) * UpscaleFactor
// The CPU sets constants so both paths use the same formula.
// Test case: PointSprites XDK sample
float DoPointSpriteSize()
{
    const PointSprite ps = state.PointSprite;

    const float A = ps.PointScaleABC.x;
    const float B = ps.PointScaleABC.y;
    const float C = ps.PointScaleABC.z;

    const float eyeDistance = length(View.Position.xyz);
    const float raw = rsqrt(A + B * eyeDistance + C * eyeDistance * eyeDistance);
    float size = raw * ps.XboxRenderTargetHeight + ps.PointSize;

    return clamp(size, ps.PointSize_Min, ps.PointSize_Max) * ps.RenderUpscaleFactor;
}

VS_OUTPUT main(const VS_INPUT xInput)
{
    VS_OUTPUT xOut;

    // Unpack 16 bool flags from 4 float4 constant registers
    vRegisterDefaultFlags = (bool[16]) vRegisterDefaultFlagsPacked;

    // Fetch all vertex attributes from ByteAddressBuffer using SV_VertexID
    {
        uint xboxVtxIdx = ResolveVertexIndex(xInput.vertexId);
        float4 vArr[16];
        FetchAllAttributes(xboxVtxIdx, vArr);
        [unroll] for (uint i = 0; i < 16u; i++) g_FetchedAttribs[i] = vArr[i];
    }

    // World + View transform with vertex blending
    View = DoTransform(Get(position), Get(normal).xyz, Get(weight));

    // Optionally normalize camera-space normals
    if (state.Modes.NormalizeNormals)
        View.Normal = normalize(View.Normal);

    // Projection transform
    // NV2A uploads CMAT (composite matrix with viewport baked in) directly.
    // CMAT × position produces screen-space coordinates. We convert
    // screen→NDC here in the shader, matching xemu's approach.
    float4 screenPos;
    if (state.Modes.UseDirectComposite) {
        // Skinning OFF: CMAT = VP × Proj × MV, multiply object-space position
        screenPos = mul(Get(position), state.Transforms.Projection);
    } else {
        // Skinning ON: CMAT = VP × Proj, multiply blended eye-space position
        screenPos = mul(View.Position, state.Transforms.Projection);
    }

    // Screen→NDC conversion (matches xemu vsh-ff.c):
    // 1. Perspective divide for xy
    // 2. Add viewport offset (half-pixel bias from NV2A XFCTX)
    // 3. Convert screen coords to NDC: (2*pos - surfaceSize) / surfaceSize
    // 4. Multiply by w to produce clip-space (D3D11 will perspective-divide)
    // 5. Normalize Z by dividing by zmax (depth range)
    // CPU guarantees SurfaceWidth, SurfaceHeight >= 1 and DepthMax > 0
    float2 surfaceSize = float2(state.Modes.SurfaceWidth, state.Modes.SurfaceHeight);
    float w = screenPos.w;
    float invW = (abs(w) > 1e-30f) ? (1.0f / w) : 0.0f;
    float2 xy = screenPos.xy * invW;                       // perspective divide
    xy += float2(state.Modes.ViewportOffsetX, state.Modes.ViewportOffsetY); // VPOFF
    xy.x = (2.0f * xy.x - surfaceSize.x) / surfaceSize.x;  // screen → NDC (X)
    xy.y = (surfaceSize.y - 2.0f * xy.y) / surfaceSize.y;  // screen → NDC (Y flipped for D3D11)
    screenPos.xy = xy * w;                                 // back to clip-space
    // Z: CMAT produces Z scaled by zmax; divide to normalize to [0,1] for D3D11 depth
    screenPos.z /= state.Modes.DepthMax;
    Projection.Position = screenPos;
    // Normal unused...

    // Projection transform - final position
    xOut.oPos = Projection.Position;

    // Diffuse and specular for when lighting is disabled
    xOut.oD0 = Get(diffuse);
    xOut.oD1 = Get(specular);
    xOut.oB0 = Get(backDiffuse);
    xOut.oB1 = Get(backSpecular);

    // Vertex lighting
    if (state.Modes.Lighting) // TODO : Remove this check by incorporating this boolean into the variables used below (set DiffuseMaterialSource to D3DMCS_COLOR1, SpecularMaterialSource to D3DMCS_COLOR2, all other to D3DMCS_MATERIAL and their colors and TotalLightsAmbient to zero, etc)
    {
        // Materials
        Material material = DoMaterial(0, diffuse, specular);
        Material backMaterial = DoMaterial(1, backDiffuse, backSpecular);

        // Compute each lighting component
        const float2 powers = float2(material.Power, backMaterial.Power);
        const LightingOutput lighting = CalcLighting(powers);

        // Frontface
        material.Specular.rgb *= lighting.Specular.Front;
        material.Diffuse.rgb *= lighting.Diffuse.Front;
        material.Ambient.rgb *= state.TotalLightsAmbient.Front;
        xOut.oD0 = float4(material.Diffuse.rgb + material.Ambient.rgb + material.Emissive.rgb, material.Diffuse.a);
        xOut.oD1 = float4(material.Specular.rgb, 0);

        if(state.Modes.TwoSidedLighting) // TODO : Same as above, for backface lighting variables
        {
            // Backface
            backMaterial.Specular.rgb *= lighting.Specular.Back;
            backMaterial.Diffuse.rgb *= lighting.Diffuse.Back;
            backMaterial.Ambient.rgb *= state.TotalLightsAmbient.Back;
            xOut.oB0 = float4(backMaterial.Diffuse.rgb + backMaterial.Ambient.rgb + backMaterial.Emissive.rgb, backMaterial.Diffuse.a);
            xOut.oB1 = float4(backMaterial.Specular.rgb, 0);
        }
    }

    // Colour
    xOut.oD0 = saturate(xOut.oD0);
    xOut.oD1 = saturate(xOut.oD1);
    xOut.oB0 = saturate(xOut.oB0);
    xOut.oB1 = saturate(xOut.oB1);

    // Fog
    xOut.oFog = DoFog();

    // Point Sprite
    xOut.oPts = DoPointSpriteSize();

    // Texture coordinates
    xOut.oT0 = DoTexCoord(0) * xboxTextureScaleRcp[0];
    xOut.oT1 = DoTexCoord(1) * xboxTextureScaleRcp[1];
    xOut.oT2 = DoTexCoord(2) * xboxTextureScaleRcp[2];
    xOut.oT3 = DoTexCoord(3) * xboxTextureScaleRcp[3];

    return xOut;
}
